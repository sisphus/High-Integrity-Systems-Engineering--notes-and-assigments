module boebert

// models data in the system
sig Data {}

// we model just two access rights, Read and Write
abstract sig Rights {}
one sig Read, Write extends Rights {}

// we model just two classifications, High and Low
abstract sig Classification {}
one sig High, Low extends Classification {}

// objects are things that have a classification
abstract sig Object {clas : one Classification}

// a capability is a special kind of data item that identifies some
// target object that it grants access to, along with a set of
// access rights that define what operations the capability authorises
// to be performed on the target object
sig Cap extends Data { target : one Object, perms : set Rights }

// objects are either memory Segments or Actors
sig Segment extends Object {}
sig Actor extends Object {}

// memory Segments hold Data, while Actors hold Capabilities
// which they can use to perform operations on memory Segments
// a State of the system models this information
one sig State {
  var segsdata : Segment -> set Data,
  var actorscaps : Actor -> set Cap
}

// the write operation involves some Actor a writing Data d to a
// memory Segment o, authorised by Capability c
// - writing data to a Segment alters no Actor's capabilities
// - it does update the data of the Segment o being written to to
//    include the Data d that is being written.
// - if the Data d being written is a Capability, then the Actor a
//    must possess that Capability (to model the idea that Caps
//    can never be forged)
// - naturally, the Actor a must possess the Capability c that is
//   authorising the write operation. this Cap must authorise
//   Writing to Segment o
pred write [s : State] {
  some a : Actor, o : Segment, d : Data, c : Cap {
    s.actorscaps' = s.actorscaps and
    s.segsdata' = s.segsdata + o -> d and
    (d in Cap implies d in s.actorscaps[a]) and
    (c in s.actorscaps[a] and o = c.target and Write in c.perms)
  }
}

// the read operation involves some Actor a reading Data d from
// a memory Segment o, authorised by Capability c
// - doing so doesn't change any Segment's Data
// - if the Data d being read is a Cap, it is added to the Actor a's
//    set of Caps that they possess; however, the Actor is allowed to
//    get the Cap in this way only if its access rights are not greater
//    than those of the Cap c that authorises the read action
// - otherwise, the Caps all Actors are unchanged
// - naturally, the Data being read must be in the Segment o,
// - and the Cap c must be possessed by the Actor a and it must
//    correctly authorise the Read of Segment o
pred read [s : State] {
 some a : Actor, o : Segment, d : Data, c : Cap {
    s.segsdata' = s.segsdata and
    s.actorscaps' = 
      (d in Cap and d.perms in c.perms => 
            s.actorscaps + a -> d 
       else 
            s.actorscaps) and
    (d in s.segsdata[o]) and
    (c in s.actorscaps[a] and o = c.target and Read in c.perms)
  }
}

// a predicate to specify what it means for a transition to occur that
// does not modify the state, i.e. that leaves the state unchanged
pred state_unchanged[s : State] {
  s.actorscaps' = s.actorscaps
  s.segsdata' = s.segsdata
}

// a state transition occurs when either a read or write operation happens
// or the state is left unchanged
pred state_transition [s : State] {
    (read [s] or write [s] or state_unchanged[s])
}


// we define what it means for a State to be "safe", which essentially
// means that it satisfies the Simple Security Property and the *-Property
// of the Bell LaPadula model:
//   1. "no read up": no Low Actor a possesses a Read Cap to a High object
//   2. "no write down": no High Actor a possesses a Write Cap to a Low object
pred safe_state [s : State] {
  (all a : Actor, c : Cap {
   c in s.actorscaps[a] implies (
   ((a.clas = Low and c.target.clas = High) implies Read not in c.perms)
   and
   ((a.clas = High and c.target.clas = Low) implies Write not in c.perms)
  ) 
 })
}

// show an example of a safe state
pred show_safe_state {
  some s : State | safe_state [s]
}
run show_safe_state for 2 expect 1


// asserts that a single write operation can never cause a safe state
// to become unsafe
assert write_safe {
  always all s : State | 
    write [s] and safe_state[s] implies after safe_state[s]
}
check write_safe for 5 but 2..2 steps expect 0
// this tells us that writes on their own can't make a state unsafe

// asserts the same thing for read operations
assert read_safe {
  always all s : State | 
    read [s] and safe_state[s] implies after safe_state[s]
}
check read_safe for 3 but 1 State expect 1
// this finds a counterexample, telling us that reads can make a
// safe state unsafe

// on its own this tell us that there might be an attack, but so
// far we don't know what the attack is.
// To have Alloy try to generate an attack, we need to talk about 
// multiple operations occurring one after the other in a sequence
// we do that as follows


// we first declare an axiom stating that the way that states
// evolve (i.e. that transitions occur) is through the state_transition
// predicate that we defined above. This essentially encodes the 
// idea that the only actions that can occur in the system are
// those defined by this predicate
// this means Alloy will consider only these kinds of traces
fact state_transition {
  always all s: State {
    state_transition[s]  
  }
}

pred show_example_execution_sequence {}

// show an execution seuqnece involving 2 states, i.e. the occurrence
// of a single operation (read or write)
run show_example_execution_sequence for 3 but 2..2 steps expect 1

// show an execution sequence involving 3 states, i.e. two 
// consecutive operations
run show_example_execution_sequence for 3 but 3..3 steps expect 1

// assert that all execution sequences beginning in a safe stete,
// always end in a safe state, i.e. can never reach an unsafe state
// Put another way, this asserts that no sequence of reads and writes
// that can occur can ever make a safe state unafe.
assert no_violation {
  all s : State {
    safe_state [s] implies always all s2: State | safe_state [s2]
  }
}

check no_violation for 3 but 2..2 steps expect 1
// this finds a counterexample, but one in which in the initial state
// there is a Segment that already holds a Cap that violates the
// spirit of the Bell-LaPadula security properties. 

// We therefore define a stronger initial condition to rule out
// this possibility.
//
// A stronger version of the safe state condition that also forbids
// High memory Segments from containing Low Write Caps and
// Low memory Segments from containing High Read Caps.
// This essentially extends the Bell-LaPadula properties to apply to
// Segments as well as Actors
pred safe_state_strong [s : State] {
  (all a : Actor, c : Cap {
   c in s.actorscaps[a] implies (
   ((a.clas = High and c.target.clas = Low) implies Write not in c.perms)
   and
   ((a.clas = Low and c.target.clas = High) implies Read not in c.perms)
  ) 
 }) and
 all a : Segment, c : Cap {
   c in s.segsdata[a] implies (
   ((a.clas = High and c.target.clas = Low) implies Write not in c.perms)
   and
   ((a.clas = Low and c.target.clas = High) implies Read not in c.perms)
  ) 
 }
}


// an assertion to try to find an attack beginning from a state
// satisfying the stronger safety condition, i.e. one that doesn't
// violate the spirit of Bell-LaPadula security policy
assert no_violation_from_strong {
  all s: State | safe_state_strong[s] implies always all s2: State | safe_state [s2]
}


// this check finds an attack analogous to Boebert's
check no_violation_from_strong for 3 expect 1

